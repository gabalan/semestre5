(* verifiez que votre fichier d'initialisation d'emacs 
  contient la ligne
(load-file "/usr/share/emacs/site-lisp/proofgeneral/generic/proof-site.el")
 *)

(* Raccourcis utiles en mode Coq: *)
(* C-c C-n "Next": avancer d'une commande *)
(* C-c C-u "Undo": reculer d'une commande *)
(* C-c C-RET "Goto": avancer/reculer jusqu'au curseur *)

(** premiers pas en Coq *)

Section Declarations.

  Variables P Q R S T : Prop.

  (* Une Section peut contenir d'autres [sous-]Sections. C'est le bon endroit
     pour definir des hypotheses (et donc prouver des sequents avec hypotheses).

     Dans ce fichier, la section "Declarations" va jusqu'au bout *)
  
  Lemma imp_dist: (P -> Q -> R) -> (P -> Q) -> P -> R.
  Proof.
    intro H.
    intro H0.
    intro H1.
    apply H.
    assumption.
    apply H0.
    assumption.
  Qed.

  (* Explication des tactiques utilisées: *)
  (* intro: utilisation de la regle d'introduction pour changer de but *)
  (* apply: utilisation d'une implication qui a la bonne conclusion
     (il va falloir prouver ses hypotheses) *)
  (* assumption: utilisation de la regle d'hypothese *)

  Check imp_dist.  (* On voit la formule prouvée *)
  Print imp_dist.  (* Pour voir le "terme de preuve" calculé *)


  (* exemple de preuve d'un sequent avec hypothèses *)

  Section S1.
    Hypothesis H : P -> Q.
    Hypothesis H0 : P -> Q -> R.

    Lemma L2 : P -> R.
    Proof.
      intro p.
      apply H0.
      assumption.
      apply H.
      assumption.
    Qed.

    Check L2. (* Les hypothèse font partie de la section *)
  End S1.

  (* Quand on ferme la section, ses hypotheses sont "exportees" sous la
     forme d'hypotheses supplementaires pour L2                         *)
  Check L2.


  
  Section About_cuts.
    Hypothesis H : P -> Q.
    Hypothesis H0 : P -> Q -> R.
    Hypothesis H1 : Q -> R -> S.
    Hypothesis H2 : Q -> R -> S -> T.

    (* preuve sans lemme (coupure) *)
    Lemma L3 : P -> T.
    Proof.
      intro p.
      apply H2.
      apply H.
      assumption.
      apply H0.
      assumption.
      apply H.
      assumption.
      apply H1.
      apply H; assumption.
      apply H0.
      assumption.
      apply H;assumption.
    Qed.

    (* preuve avec coupures *)

     Lemma L'3 : P -> T.
       intro p.
       assert (q: Q).  {
         apply H; assumption.
        }
       assert (r: R). {
         apply H0.
         assumption.
         assumption.
       }
       assert (s : S). {
        apply H1; assumption.
       }
       apply H2; assumption.
     Qed.

     (* assert: permet d'ouvrir une nouvelle sous-preuve, *)
     (* dans laquelle on se définit un nouveau but; c'est *)
     (* une coupure. Les accolades sont optionnelles mais *)
     (* facilitent grandement la lecture humaine          *)
     
     Check L'3.
     Print L'3.

  End About_cuts.


 (* Exercices 

    Reprendre les exemples vus en TD, en utilisant les tactiques 
    assumption, apply et intros; vous pouvez occasionnellement 
    faire des coupures.

    Remplacer chaque commande Admitted par une preuve correcte 
    suivie de la commande Qed.

  *)

  Lemma IdP : P -> P.
  Proof.
    intro H.
    assumption.
  Qed.

  Lemma IdPP : (P -> P) -> P -> P.
  Proof.
    intros h0 h1 .
    assumption .
 Qed.

  Lemma imp_trans : (P -> Q) -> (Q -> R) -> P -> R.
  Proof.
    intros h0 h1 h2 .
    apply h1.
    apply h0.
    assumption .
 Qed.

  Section proof_of_hyp_switch.
    Hypothesis H : P -> Q -> R.
    Lemma hyp_switch : Q -> P -> R.
    Proof.
      intros h0 h1.
      apply H.
      assumption .
      assumption .
      
      
   Qed.

  End proof_of_hyp_switch.

  Check hyp_switch.

  Section proof_of_add_hypothesis.
    Hypothesis H : P -> R.

    Lemma add_hypothesis : P -> Q -> R.
    Proof.
      intros .
      apply H.
      assumption .
   Qed .
  End proof_of_add_hypothesis.

  (* prouver le sequent (P -> P -> Q) |- P -> Q  *)
  Section S.
    Hypothesis H: P->P->Q.
    Lemma hhh :P->Q.
    Proof .
      intros .
      apply H.
      assumption .
      assumption .
      Qed.
End S. 
  (* meme exercice avec 
     P -> Q , P -> R , Q -> R -> T |- P -> T  
   *)
  Section S4.
    Hypothesis H0:P->Q.
    Hypothesis H1:P->R.
    Hypothesis H2: Q->R->T.
    Lemma hhhhh:P->T.
    Proof .
      intros  .
      apply H2.
      apply H0.
      assumption .
      apply H1.
      assumption .
      Qed.
  End S4.
  Lemma weak_Peirce : ((((P -> Q) -> P)-> P) -> Q) -> Q.
  Proof.
    intros .
    apply H .
    intros .
    apply H0.
    intros .
    apply H.
    intros .
    assumption .
 Qed .

End Declarations.

Check hyp_switch.


  
     